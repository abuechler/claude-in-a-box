#!/usr/bin/env zsh
###########################################################
# Claude In A Box - Command Line Launcher
#
# Basically launches Claude Code in a Docker container and mounts the project
# directory plus some settings. Unfortunately Claude Code doesn't store
# settings in a way that is easy to work with, there are several locations,
# from the container POV they are:
#
# - $HOME/.claude
# - $HOME/.claude/settings.json
# - $HOME/.claude.json
# - <project>/.claude/settings.json
# - <project>/.claude/settings.local.json
#
# Those inside the project directory are easy to handle, we can just add them
# to Git if we want to. The $HOME/.claude directory with the
# $HOME/.claude/settings.json is easy too, since the settings.json is simple
# and doesn't need to contain generated data, or even secrets.
#
# The tricky part is actually the $HOME/.claude.json file, if we just mount an
# empty file, claude code will refuse to start and report a corrupted settings
# file. Which it can fix by itself, but still it is ugly. Generating one is
# possible, but should we really? Let's see how brittle that is and generate
# one for now.
##########################################################
set -euo pipefail
version="1.0.1"

script_dir=$(dirname "$(realpath "$0")")

project_directory="$(pwd)"
# Derive the project name from the directory name, we'll sanitize it later in read_claude_in_a_box_settings
project_name="$(basename "$project_directory")"
# Contains files and settings for all projects
claude_project_settings_root="$HOME/.claude_project_settings"
# Project specific settings directory => All the stuff we want to persist per project across container runs
claude_project_settings="$claude_project_settings_root/$project_name"
# Claude settings directory for this project => Mounted to /home/node/.claude in the container
claude_project_dot_dir="$claude_project_settings/claude"
# Claude settings JSON file for this project => Mounted to /home/node/.claude.json in the container
claude_project_dot_json="$claude_project_settings/claude.json"
# Shell history file for this project, see Dockerfile => There's going to be intermediate files in this dir as well, that's why we put all of them in a separate dir.
claude_project_shell_history_dir="$claude_project_settings/shell_history"
claude_project_shell_history_file="$claude_project_shell_history_dir/.zsh_history"
# Generic Claude In A Box settings for all projects. If this folder contains an init.sh script that is executable, it will be run at container startup.
claude_in_a_box_global_settings="$HOME/.claude-in-a-box"
# GitHub token for this project, if it exists Git credentials will be generated.
gh_project_token_file="$claude_project_settings/.gh_token"

docker_image="claude-in-a-box"

read_claude_in_a_box_settings() {
  # We're assuming that claude-in-a-box should be started from the current directory, let's do some sanity checks.
  if [[ "$project_directory" == "$HOME" || "$project_directory" == "/" || ! -d "$project_directory/.git" ]]; then
    echo "‚ùå Refusing to run in \$HOME, root or any directory that is not a Git project."
    exit 1
  fi

  # Create project settings directory if it doesn't exist
  if [[ -d "$claude_project_dot_dir" ]]; then
    echo -e "\t‚úÖ Found Claude project settings in '$claude_project_dot_dir'."
    # Add settings file on existing projects if missing
    if [[ ! -f "$claude_project_dot_json/settings.json" ]]; then
      cp "$script_dir/init_config/settings.json" "$claude_project_dot_dir"
    fi
  else
    echo -e "\tüìÅ Creating project settings directory..."
    mkdir -p "$claude_project_dot_dir"
    cp "$script_dir/init_config/settings.json" "$claude_project_dot_dir"
    # Generate a unique userID (64 hex chars) using only default tools
    # available on macOS and Linux, see comments at the beginning of this script.
    user_id=$(od -An -N32 -tx1 /dev/urandom | tr -d ' \n')
    # Copy claude.json and update the userID field using sed
    sed "s/\"userID\": null/\"userID\": \"$user_id\"/" "$script_dir/init_config/claude.json" >"$claude_project_dot_json"
  fi

  mkdir -p "$claude_project_shell_history_dir"
  if [[ ! -f "$claude_project_shell_history_file" ]]; then
    touch "$claude_project_shell_history_file"
  fi

  # Determine which GitHub token to use
  if [[ -s "$gh_project_token_file" ]]; then
    gh_token_file="$gh_project_token_file"
    echo -e "\t‚úÖ Using project-specific GitHub token for project '$project_name'."
  else
    echo -e "\t‚ÑπÔ∏è No GitHub token found. To use gh CLI, create a fained grained token and save it to:"
    echo -e "\tüëâ $gh_project_token_file"
    # Create empty token file to prevent mount errors
    touch "$gh_project_token_file"
    gh_token_file="$gh_project_token_file"
  fi

  if [[ ! -d "$claude_in_a_box_global_settings" ]]; then
    echo -e "\tüìÅ Creating user settings directory in $claude_in_a_box_global_settings"
    mkdir -p "$claude_in_a_box_global_settings"
  fi

  if [[ ! -f .mcp.json ]]; then
    echo -e "\t‚ÑπÔ∏è No .mcp.json file found in project directory. Creating one with Playwright settings..."
    cp "$script_dir/init_config/mcp.json" "$project_directory/.mcp.json"
    echo -e "\tüí°You might want to add the $project_directory/.mcp.json file to Git and commit it."
  fi

  if [[ -f "$project_directory/.claude_in_a_box" ]]; then
    echo -e "\t‚úÖ Found .claude_in_a_box settings in project directory."
  else
    echo -e "\t‚ÑπÔ∏è No .claude_in_a_box file found in project directory. Creating one..."
    safe_project_name=$(echo "$project_name" | tr -cd 'A-Za-z0-9_-')
    echo "project_name=\"$safe_project_name\"" >"$project_directory/.claude_in_a_box"
    echo -e "\tüí°You might want to add the $project_directory/.claude_in_a_box file to Git and commit it."
  fi
  source "$project_directory/.claude_in_a_box"
}

run_claude_code() {
  echo "üöÄ Running Claude Code for project '$project_name'..."
  read_claude_in_a_box_settings
  docker run -ti --rm --name "claude-in-a-box-$project_name" \
    --entrypoint "/bin/zsh" \
    -v "$project_directory:/workspace" \
    -v "$gh_token_file:/home/node/.gh_token:ro" \
    -v "$claude_project_dot_dir:/home/node/.claude" \
    -v "$claude_project_dot_json:/home/node/.claude.json" \
    -v "$claude_project_shell_history_dir:/home/node/.shell_history" \
    -v "$claude_in_a_box_global_settings:/home/node/.claude-in-a-box:ro" \
    "$docker_image":$version
}

build_custom_docker_image() {
  set +e
  docker info &>/dev/null || {
    echo "‚ùå Docker is not installed or not running. Please install or start Docker to use this script."
    exit 1
  }

  if ! docker image inspect "$docker_image":$version &>/dev/null; then
    set -e
    echo -e "üîß  Building Docker image '$docker_image'..."
    current_timezone="$(readlink /etc/localtime | sed 's|.*/zoneinfo/||')"
    echo "üîç  Looking for Dockerfile in '$script_dir'..."
    (
      cd "$script_dir/docker"
      # Pass the timezone as a build argument
      docker build --no-cache --build-arg TZ="$current_timezone" -t "$docker_image":$version .
    )
  fi
  set -e
}

build_custom_docker_image
run_claude_code
